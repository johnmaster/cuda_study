#include <algorithm>
#include <cuda_bf16.h>
#include <cuda_fp16.h>
#include <cuda_fp8.h>
#include <cuda_runtime.h>
#include <float.h>
#include <stdio.h>
#include <stdlib.h>
#include <torch/extension.h>
#include <torch/types.h>
#include <vector>

#define WARP_SIZE 32
#define FLOAT4(value) (reinterpret_cast<float4 *>(&(value))[0])
#define HALF2(value) (reinterpret_cast<half2 *>(&(value))[0])
#define LDST128BITS(value) (reinterpret_cast<float4 *>(&(value))[0])

template <const int kWarpSize = WARP_SIZE>
__device__ __forceinline__ float warp_reduce_sum_f32(float val) {
    for (int mask = kWarpSize >> 1; mask >= 1; mask >>= 1)
        val += __shfl_xor_sync(0xffffffff, val, mask);
    return val;
}

template <const int NUM_THREADS = 256>
__global__ void dot_prod_f32_f32_kernel(float *a, float *b, float *y, int N) {
    int tid = threadIdx.x;
    int idx = blockIdx.x * NUM_THREADS + tid;
    constexpr int NUM_WARPS = (NUM_THREADS + WARP_SIZE - 1) / WARP_SIZE;
    __shared__ float reduce_smem[NUM_WARPS];
    float prod = (idx < N) ? a[idx] * b[idx] : 0.0f;
    int warp = tid / WARP_SIZE;
    int lane = tid % WARP_SIZE;
    prod = warp_reduce_sum_f32<WARP_SIZE>(prod);
    if (lane == 0)
        reduce_smem[warp] = prod;
    __syncthreads();
    prod = (lane < NUM_WARPS) ? reduce_smem[lane] : 0.0f;
    if (warp == 0)
        prod = warp_reduce_sum_f32<NUM_WARPS>(prod);
    if (tid == 0)
        atomicAdd(y, prod);
}

template <const int NUM_THREADS = 256 / 4>
__global__ void dot_prod_f32x4_f32_kernel(float *a, float *b, float *y, int N) {
    int tid = threadIdx.x;
    int idx = (blockIdx.x * NUM_THREADS + tid) * 4;
    constexpr int NUM_WARPS = (NUM_THREADS + WARP_SIZE - 1) / WARP_SIZE;
    __shared__ float reduce_smem[NUM_WARPS];
    
    float4 reg_a = FLOAT4(a[idx]);
    float4 reg_b = FLOAT4(b[idx]);
    float prod = (idx < N) ? (reg_a.x * reg_b.x + reg_a.y * reg_b.y +
                              reg_a.z * reg_b.z + reg_a.w * reg_b.w) : 0.0f;
    int warp = tid / WARP_SIZE;
    int lane = tid % WARP_SIZE;
    prod = warp_reduce_sum_f32<WARP_SIZE>(prod);
    if (lane == 0)
        reduce_smem[warp] = prod;
    __syncthreads();
    prod = (lane < NUM_WARPS) ? reduce_smem[lane] : 0.0f;
    if (warp == 0)
        prod = warp_reduce_sum_f32<NUM_WARPS>(prod);
    if (tid == 0)
        atomicAdd(y, prod);
}

template <const int kWarpSize = WARP_SIZE>
__device__ __forceinline__ half warp_reduce_sum_f16_f16(half val) {
    for (int mask = kWarpSize >> 1; mask >= 1; mask >>= 1)
        val = __hadd(val, __shfl_xor_sync(0xffffffff, val, mask));
    return val;
}

template <const int kWarpSize = WARP_SIZE>
__device__ __forceinline__ float warp_reduce_sum_f16_f32(half val) {
    float val_f32 = __half2float(val);
    for (int mask = kWarpSize >> 1; mask >= 1; mask >>= 1)
        val_f32 += __shfl_xor_sync(0xffffffff, val_f32, mask);
    return val_f32;
}

template <const int NUM_THREADS = 256>
__global__ void dot_prod_f16_f32_kernel(half *a, half *b, float *y, int N) {
    int tid = threadIdx.x;
    int idx = blockIdx.x * NUM_THREADS + tid;
    constexpr int NUM_WARPS = (NUM_THREADS + WARP_SIZE - 1) / WARP_SIZE;
    __shared__ float reduce_smem[NUM_WARPS];
    
    half prod_f16 = (idx < N) ? __hmul(a[idx], b[idx]) : __float2half(0.0f);
    int warp = tid / WARP_SIZE;
    int lane = tid % WARP_SIZE;
    float prod = warp_reduce_sum_f16_f32<WARP_SIZE>(prod_f16);
    if (lane == 0)
        reduce_smem[warp] = prod;
    __syncthreads();
    prod = (lane < NUM_WARPS) ? reduce_smem[lane] : 0.0f;
    if (warp == 0)
        prod = warp_reduce_sum_f32<WARP_SIZE>(prod);
    if (tid == 0)
        atomicAdd(y, prod);
}

template <const int NUM_THREADS = 256 / 2>
__global__ void dot_prod_f16x2_f32_kernel(half *a, half *b, float *y, int N) {
    int tid = threadIdx.x;
    int idx = (blockIdx.x * NUM_THREADS + tid) * 2;
    constexpr int NUM_WARPS = (NUM_THREADS + WARP_SIZE - 1) / WARP_SIZE;
    __shared__ float reduce_smem[NUM_WARPS];
    
    half2 reg_a = HALF2(a[idx]);
    half2 reg_b = HALF2(b[idx]);
    half prod_f16 = (idx < N) ? __hadd(__hmul(reg_a.x, reg_b.x), __hmul(reg_a.y, reg_b.y)) : __float2half(0.0f);
    int warp = tid / WARP_SIZE;
    int lane = tid % WARP_SIZE;
    float prod = warp_reduce_sum_f16_f32<WARP_SIZE>(prod_f16);
    if (lane == 0)
        reduce_smem[warp] = prod;
    __syncthreads();
    prod = (lane < NUM_WARPS) ? reduce_smem[lane] : 0.0f;
    if (warp == 0)
        prod = warp_reduce_sum_f32<NUM_WARPS>(prod);
    if (tid == 0)
        atomicAdd(y, prod);
}

template <const int NUM_THREADS = 256 / 8>
__global__ void dot_prod_f16x8_pack_f32_kernel(half *a, half *b, float *y, int N) {
    int tid = threadIdx.x;
    int idx = (blockIdx.x * NUM_THREADS + tid) * 8;
    constexpr int NUM_WARPS = (NUM_THREADS + WARP_SIZE - 1) / WARP_SIZE;
    __shared__ float reduce_smem[NUM_WARPS];
    half pack_a[8], pack_b[8];
    LDST128BITS(pack_a[0]) = LDST128BITS(a[idx]);
    LDST128BITS(pack_b[0]) = LDST128BITS(b[idx]);
    const half z = __float2half(0.0f);
    half prod_f16 = z;
    for (int i = 0; i < 8; i += 2) {
        half2 v = __hmul2(HALF2(pack_a[i]), HALF2(pack_b[i]));
        prod_f16 += (((idx + i) < N) ? (v.x + v.y) : z);
    }
    
    int warp = tid / WARP_SIZE;
    int lane = tid % WARP_SIZE;
    float prod = warp_reduce_sum_f16_f32<WARP_SIZE>(prod_f16);
    if (lane == 0)
        reduce_smem[warp] = prod;
    __syncthreads();
    prod = (lane < NUM_WARPS) ? reduce_smem[lane] : 0.0f;
    if (warp == 0)
        prod = warp_reduce_sum_f32<NUM_WARPS>(prod);
    if (tid == 0)
        atomicAdd(y, prod);
}

#define STRINGFY(str) #str
#define TORCH_BINDING_COMMON_EXTENSION(func)        \
    m.def(STRINGFY(func), &func, STRINGFY(func));
#define CHECK_TORCH_TENSOR_DTYPE(T, th_type)                        \
    if (((T).options().dtype() != (th_type)))   {                   \
        std::cout << "Tensor Info:" << (T).options() << std::endl;  \
        throw std::runtime_error("values must be " #th_type);       \
    }

#define LAUNCH_DOT_PROD_KERNEL(NT, packed_type, acc_type, element_type)     \
    dot_prod_##packed_type##_##acc_type##_kernel<(NT)>                      \
        <<<grid, block>>>(reinterpret_cast<element_type *>(a.data_ptr()),   \
                          reinterpret_cast<element_type *>(b.data_ptr()),   \
                          prod.data_ptr<float>(), N);

#define DISPATCH_DOT_PROD_KERNEL(K, packed_type, acc_type, element_type,    \
                                 n_elements)                                \
    const int NT = (K) / (n_elements);                                      \
    dim3 block(NT);                                                         \
    dim3 grid((S));                                                         \
    switch (NT) {                                                           \
    case 32:                                                                \
        LAUNCH_DOT_PROD_KERNEL(32, packed_type, acc_type, element_type)     \
        break;                                                              \
    case 64:                                                                \
        LAUNCH_DOT_PROD_KERNEL(64, packed_type, acc_type, element_type)     \
        break;                                                              \
    case 128:                                                               \
        LAUNCH_DOT_PROD_KERNEL(128, packed_type, acc_type, element_type)    \
        break;                                                              \
    case 256:                                                               \
        LAUNCH_DOT_PROD_KERNEL(256, packed_type, acc_type, element_type)    \
        break;                                                              \
    case 512:                                                               \
        LAUNCH_DOT_PROD_KERNEL(512, packed_type, acc_type, element_type)    \
        break;                                                              \
    case 1024:                                                              \
        LAUNCH_DOT_PROD_KERNEL(1024, packed_type, acc_type, element_type)   \
        break;                                                              \
    default:                                                                \
        throw std::runtime_error(                                           \
            "only support (K) / (n_elements): 32 64 128 256 512 1024"       \
        );                                                                  \
        break;                                                              \
    }      

#define TORCH_BINDING_DOT_PROD(packed_type, acc_type, th_type, element_type,            \
                               n_elements)                                              \
    torch::Tensor dot_prod_##packed_type##_##acc_type(torch::Tensor a,                  \
                                                      torch::Tensor b) {                \
        CHECK_TORCH_TENSOR_DTYPE(a, (th_type))                                          \
        CHECK_TORCH_TENSOR_DTYPE(b, (th_type))                                          \
        auto options =                                                                  \
            torch::TensorOptions().dtype(torch::kFloat32).device(torch::kCUDA, 0);      \
        auto prod = torch::zeros({1}, options);                                         \
        const int ndim = a.dim();                                                       \
        if (ndim != 2) {                                                                \
            int N = 1;                                                                  \
            for (int i = 0; i < ndim; i++)                                              \
                N *= a.size(i);                                                         \
            dim3 block(256);                                                            \
            dim3 grid(((N + 256 - 1) / 256) / (n_elements));                            \
            dot_prod_##packed_type##_##acc_type##_kernel<256>                           \
                <<<grid, block>>>(reinterpret_cast<element_type *>(a.data_ptr()),       \
                                  reinterpret_cast<element_type *>(b.data_ptr()),       \
                                  prod.data_ptr<float>(), N);                           \
        } else {                                                                        \
            const int S = a.size(0);                                                    \
            const int K = a.size(1);                                                    \
            const int N = S * K;                                                        \
            if ((K / n_elements) <= 1024) {                                               \
                DISPATCH_DOT_PROD_KERNEL(K, packed_type, acc_type, element_type,        \
                                         n_elements)                                    \
            } else {                                                                      \
                int N = 1;                                                              \
                for (int i = 0; i < ndim; i++)                                          \
                    N *= a.size(i);                                                     \
                dim3 block(256);                                                        \
                dim3 grid(((N + 256 - 1) / 256) / (n_elements));                        \
                dot_prod_##packed_type##_##acc_type##_kernel<256>                       \
                    <<<grid, block>>>(reinterpret_cast<element_type *>(a.data_ptr()),   \
                                      reinterpret_cast<element_type *>(b.data_ptr()),   \
                                      prod.data_ptr<float>(), N);                       \
            }                                                                           \
        }                                                                               \
        return prod;                                                                    \
    }

TORCH_BINDING_DOT_PROD(f32, f32, torch::kFloat32, float, 1)
TORCH_BINDING_DOT_PROD(f32x4, f32, torch::kFloat32, float, 4)
TORCH_BINDING_DOT_PROD(f16, f32, torch::kHalf, half, 1)
TORCH_BINDING_DOT_PROD(f16x2, f32, torch::kHalf, half, 2)
TORCH_BINDING_DOT_PROD(f16x8_pack, f32, torch::kHalf, half, 8)

PYBIND11_MODULE(TORCH_EXTENSION_NAME, m) {
  TORCH_BINDING_COMMON_EXTENSION(dot_prod_f32_f32)
  TORCH_BINDING_COMMON_EXTENSION(dot_prod_f32x4_f32)
  TORCH_BINDING_COMMON_EXTENSION(dot_prod_f16_f32)
  TORCH_BINDING_COMMON_EXTENSION(dot_prod_f16x2_f32)
  TORCH_BINDING_COMMON_EXTENSION(dot_prod_f16x8_pack_f32)
}